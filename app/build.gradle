repositories {
    mavenCentral()
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdk 35

    defaultConfig {
        applicationId 'br.com.smbstore.ntfy'
        minSdk 28
        targetSdk 35

        versionCode 114
        versionName '1.1.4'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        /* Required for Room schema migrations */
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    flavorDimensions "store"
    productFlavors {
        play {
            buildConfigField 'boolean', 'FIREBASE_AVAILABLE', 'true'
            buildConfigField 'boolean', 'RATE_APP_AVAILABLE', 'true'
            buildConfigField 'boolean', 'INSTALL_PACKAGES_AVAILABLE', 'false'
        }
        fdroid {
            buildConfigField 'boolean', 'FIREBASE_AVAILABLE', 'false'
            buildConfigField 'boolean', 'RATE_APP_AVAILABLE', 'false'
            buildConfigField 'boolean', 'INSTALL_PACKAGES_AVAILABLE', 'true'
        }
    }

    compileOptions {
        sourceCompatibility "11"
        targetCompatibility "11"
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        freeCompilerArgs += [
            '-Xjvm-default=all-compatibility' // https://stackoverflow.com/a/71234042/1440785
        ]
    }

    kotlin {
        jvmToolchain(11)
    }

    namespace 'br.com.smbstore.ntfy'
    buildToolsVersion '35.0.1'
}

// Disables GoogleServices tasks for F-Droid variant
android.applicationVariants.all { variant ->
    def shouldProcessGoogleServices = variant.flavorName == "play"
    def googleTask = tasks.findByName("process${variant.name.capitalize()}GoogleServices")
    googleTask.enabled = shouldProcessGoogleServices
}

// Strips out REQUEST_INSTALL_PACKAGES permission for Google Play variant
android.applicationVariants.all { variant ->
    def shouldStripInstallPermission = variant.flavorName == "play"
    if (shouldStripInstallPermission) {
        variant.outputs.each { output ->
            def processManifest = output.getProcessManifestProvider().get()
            processManifest.doLast { task ->
                def outputDir = task.getMultiApkManifestOutputDirectory().get().asFile
                def manifestOutFile = file("$outputDir/AndroidManifest.xml")
                def newFileContents = manifestOutFile.collect { s -> s.contains("android.permission.REQUEST_INSTALL_PACKAGES") ? "" : s }.join("\n")
                manifestOutFile.write(newFileContents, 'UTF-8')
            }
        }
    }
}

dependencies {
    // AndroidX, The Basics
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.activity:activity-ktx:1.10.0'
    implementation 'androidx.fragment:fragment-ktx:1.8.5'
    implementation 'androidx.work:work-runtime-ktx:2.10.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'

    // JSON serialization
    implementation 'com.google.code.gson:gson:2.12.0'

    // Room (SQLite)
    def room_version = '2.5.2'
    implementation "androidx.room:room-ktx:2.6.1"
    kapt "androidx.room:room-compiler:2.6.1"

    // OkHttp (HTTP library)
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'

    // Firebase, sigh ... (only Google Play)
    playImplementation 'com.google.firebase:firebase-messaging:24.1.0'

    // RecyclerView
    implementation 'androidx.recyclerview:recyclerview:1.4.0'

    // Swipe down to refresh
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Material design
    implementation 'com.google.android.material:material:1.12.0'

    // LiveData
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.7'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Image viewer
    implementation 'com.github.stfalcon-studio:StfalconImageViewer:v1.0.1'

    // Zxing QRcode Reader
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
}
